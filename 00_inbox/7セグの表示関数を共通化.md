実験日：2025/12/22

[[ボタンでLEDを操る|前回]]までの実験で、[[7セグメント]]に表示させるために以下のような関数を作ったが、問題点がある。
```cpp
void show3() {
  digitalWrite(a,HIGH);
  digitalWrite(b,HIGH);
  digitalWrite(c,HIGH);
  digitalWrite(d,HIGH);
  digitalWrite(e,LOW);
  digitalWrite(f,LOW);
  digitalWrite(g,HIGH);
}
```

- 数字が増えるたびに関数が増える
	- 桁が増えて11を表示したいとき、show11()という関数を作る。これが無限に続く。こんなの保守できない。

## どうにかしよう

以下のようにしてみる
```txt
1 = 0110000
2 = 1101101
3 = 1111001
8 = 1111111
```

これは、縦が数字、横がセグメントのテーブルになっている。

| 数字  | a   | b   | c   | d   | e   | f   | g   |
| --- | --- | --- | --- | --- | --- | --- | --- |
| 1   | 0   | 1   | 1   | 0   | 0   | 0   | 0   |
| 2   | 1   | 1   | 0   | 1   | 1   | 0   | 1   |
| 3   | 1   | 1   | 1   | 1   | 0   | 0   | 1   |
| ... |     |     |     |     |     |     |     |
| 8   | 1   | 1   | 1   | 1   | 1   | 1   | 1   |

テーブル構造をコードに落とし込む。
```cpp
// 数字*10 と セグメント*7
const byte digits[10][7] = {
  // a b c d e f g
  {1,1,1,1,1,1,0}, // 0
  {0,1,1,0,0,0,0}, // 1
  {1,1,0,1,1,0,1}, // 2
  {1,1,1,1,0,0,1}, // 3
  {0,1,1,0,0,1,1}, // 4
  {1,0,1,1,0,1,1}, // 5
  {1,0,1,1,1,1,1}, // 6
  {1,1,1,0,0,0,0}, // 7
  {1,1,1,1,1,1,1}, // 8
  {1,1,1,1,0,1,1}  // 9
};
```

これを設定することで、使う側のコードがシンプルになる。

```cpp
// 左から、a用のピン、b用のピン、のように並んでいる
int segPins[7] = {4,5,8,7,6,3,2};

void showDigit(int n) {
	for (int i = 0; i < 7; i++) {
		digitalWrite(segPins[i], digits[n][i] ? HIGH : LOW);
	}
}
```
例えば1を表示させるなら、
- `i = 0` のとき
- `segPins[0] = 4`（aセグのピン）
- `digits[1][0] = 0`
- → aセグは消灯

## 実験は成功だ！

最終的にコードはかなりスッキリした。
やはり私の考えは間違ってなかった。
```cpp
// 左から、a用のピン、b用のピン、のように並んでいる
int segPins[7] = {4,5,8,7,6,3,2};
int buttonPin = 9;

void setup() {
  pinMode(9, INPUT_PULLUP);
  for (int i=0; i<7; i++) {
    pinMode(segPins[i], OUTPUT);
    digitalWrite(segPins[i], LOW); // まず全部消す
  }
}

int count=0;
int lastButtonState = HIGH;
void loop() {
  int currentState = digitalRead(buttonPin);
  if (lastButtonState == HIGH && currentState == LOW) {
    delay(50); // チャタリング待ち
    if (digitalRead(buttonPin) == LOW) { // 待ったあとでも押されているなら本物とする
      count++;
      if (count > 9) {
        count = 0;
      }
    }
  }
  lastButtonState = currentState;

  showDigit(count);
}

// 数字*10 と セグメント*7
const byte digits[10][7] = {
  // a b c d e f g
  {1,1,1,1,1,1,0}, // 0
  {0,1,1,0,0,0,0}, // 1
  {1,1,0,1,1,0,1}, // 2
  {1,1,1,1,0,0,1}, // 3
  {0,1,1,0,0,1,1}, // 4
  {1,0,1,1,0,1,1}, // 5
  {1,0,1,1,1,1,1}, // 6
  {1,1,1,0,0,0,0}, // 7
  {1,1,1,1,1,1,1}, // 8
  {1,1,1,1,0,1,1}  // 9
};

void showDigit(int n) {
	for (int i = 0; i < 7; i++) {
		digitalWrite(segPins[i], digits[n][i] ? HIGH : LOW);
	}
}
```
